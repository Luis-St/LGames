plugins {
	id "java"
	id "org.openjfx.javafxplugin" version "0.0.13"
}

repositories {
	mavenCentral()
	maven {
		url "https://libraries.minecraft.net"
	}
	maven {
		url uri("${projectDir}/maven/repositories")
	}
}

javafx {
	version = "17"
	modules = ["javafx.controls"]
}

dependencies {
	// Modules
	implementation project(":account")
	implementation project(":client")
	implementation project(":common")
	implementation project(":game")
	implementation project(":language")
	implementation project(":ludo")
	implementation project(":network")
	implementation project(":server")
	implementation project(":ttt")
	implementation project(":wins4")
	// Local
	implementation "org.controlsfx:ControlsFX:1.1" // Fx
	implementation "net.luis:LUtils:2.0" // Utility
	implementation "net.luis:FxUtils:2.2" // Fx Utility
	// Google
	implementation "com.google.guava:guava:31.0.1-jre" // Utility
	implementation "com.google.code.gson:gson:2.8.9" // Json
	// Apache
	implementation "org.apache.logging.log4j:log4j-api:2.14.1" // Logging
	implementation 'org.apache.logging.log4j:log4j-core:2.17.1' // Logging
	implementation "org.apache.commons:commons-lang3:3.0" // Utility
	implementation "org.apache.commons:commons-email:1.5" // E-Mail
	implementation "commons-codec:commons-codec:1.15" // Cryptic
	// Netty
	implementation 'io.netty:netty-all:4.1.25.Final' // Network
	// Other
	implementation "net.sf.jopt-simple:jopt-simple:5.0.4" // Startup
	implementation "com.mojang:datafixerupper:4.0.26" // Codec/Json helper
	implementation "org.jetbrains:annotations:23.0.0" // Annotations
	// Test
	testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.1"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.1"
}

test {
	useJUnitPlatform()
}

tasks.register("runClient", JavaExec) {
	group = "runs"
	getMainClass().set("net.luis.Main")
	classpath = sourceSets.main.runtimeClasspath
	enableAssertions = true
	args("--debugMode", "false", "--ide", "--client", "--gameDir", file("run/client"), "--resourceDir", file("assets"), "--accountHost", "localhost", "--accountPort", "8081")
	args("--instantLoading", "true", "--safeLoading", "false", "--cachePasswordLocal", "true")
	jvmArgs("--module-path", classpath.asPath, "--add-modules", "javafx.controls")
}

tasks.register("runServer", JavaExec) {
	group = "runs"
	getMainClass().set("net.luis.Main")
	classpath = sourceSets.main.runtimeClasspath
	enableAssertions = true
	args("--debugMode", "false", "--ide", "--server", "--gameDir", file("run/server"), "--resourceDir", file("assets"), "--host", "localhost", "--port", "8080", "--language", "en_us")
	args("--admin", "00000000-0000-014b-0000-00000000017d")
	jvmArgs("--module-path", classpath.asPath, "--add-modules", "javafx.controls")
}

tasks.register("runAccount", JavaExec) {
	group = "runs"
	getMainClass().set("net.luis.Main")
	classpath = sourceSets.main.runtimeClasspath
	enableAssertions = true
	args("--debugMode", "false", "--ide", "--account", "--gameDir", file("run/account_server"), "--resourceDir", file("assets"), "--host", "localhost", "--port", "8081", "--language", "en_us")
	jvmArgs("--module-path", classpath.asPath, "--add-modules", "javafx.controls")
}

tasks.register("runTest", JavaExec) {
	group = "test runs"
	getMainClass().set("net.luis.Test")
	classpath = sourceSets.main.runtimeClasspath
	enableAssertions = true
	jvmArgs("--module-path", classpath.asPath, "--add-modules", "javafx.controls")
}

tasks.register("runFxTest", JavaExec) {
	group = "test runs"
	getMainClass().set("net.luis.FxTest")
	classpath = sourceSets.main.runtimeClasspath
	enableAssertions = true
	jvmArgs("--module-path", classpath.asPath, "--add-modules", "javafx.controls")
}

compileJava {
	options.compilerArgs << "-parameters"
}

jar {
	duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
	manifest {
		attributes([
				"Main-Class"   : "net.vgc.Main",
				"Built-By"     : "Luis Staudt, (Felix Stenzel and Luca Loos)",
				"Multi-Release": "true"
		])
	}
	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
}
